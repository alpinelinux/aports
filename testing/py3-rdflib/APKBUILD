# Maintainer: Luca Weiss <luca@z3ntu.xyz>
pkgname=py3-rdflib
_pyname=rdflib
pkgver=4.2.2
pkgrel=0
pkgdesc="A Python library for working with RDF, a simple yet powerful language for representing information"
url="https://rdflib.readthedocs.org/"
arch="noarch"
license="BSD-3-Clause"
depends="py3-isodate py3-six"
checkdepends="py3-nose py3-parsing py3-html5lib py3-sparqlwrapper"
makedepends="py3-setuptools"
_pypiprefix="${_pyname%${_pyname#?}}"
source="https://files.pythonhosted.org/packages/source/$_pypiprefix/$_pyname/$_pyname-$pkgver.tar.gz
	py3.7.patch"
builddir="$srcdir/$_pyname-$pkgver"

build() {
	python3 setup.py build
}

check() {
	# "python3 ./run_tests.py" fails
	nosetests --where=./build/src \
		--with-doctest \
		--doctest-extension=.doctest \
		--doctest-tests
}

package() {
	python3 setup.py install --prefix=/usr --root="$pkgdir"
}

sha512sums="efc24d8ce1080e59950cecc9c779ae0d63673c8690f74db1772f6c2dbf24d6f4de5214d5870a074bffe549b42d8d5f5d131df29bbfd174b763ae1f843aec7d5c  rdflib-4.2.2.tar.gz
a6e847bff7ab7e3e490dd86424bd9ec618722d8010b5373ab4848f2d3187edb49f7dce7d30c8738e0596759e77d27132e1ca114fccdf554da4af56bc23c3c2bf  py3.7.patch"
