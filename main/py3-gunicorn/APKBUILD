# Contributor: <xmingske@gmail.com>
# Maintainer: Natanael Copa <ncopa@alpinelinux.org>
pkgname=py3-gunicorn
_pkgname=gunicorn
pkgver=19.9.0
pkgrel=0
pkgdesc="a Python WSGI HTTP Server"
url="https://github.com/benoitc/gunicorn/"
arch="noarch"
license="MIT"
depends="py3-setuptools"
checkdepends="py3-pytest py3-pytest-cov"
source="$_pkgname-$pkgver.tar.gz::https://github.com/benoitc/$_pkgname/archive/${pkgver}.tar.gz"

builddir="${srcdir}/${_pkgname}-${pkgver}"

options="!check" # py3-pytest-cov is in community

replaces=py-gunicorn # Backwards compatibility
provides=py-gunicorn=$pkgver-r$pkgrel # Backwards compatibility

prepare() {
	default_prepare
	# use installed checkdepends instead of fetching from pypi
	sed -i \
		-e 's/==.*//' \
		-e '/cover/d' \
		requirements_test.txt
}

build() {
	python3 setup.py build
}

check() {
	python3 setup.py test
}

package() {
	python3 setup.py install --prefix=/usr --root="$pkgdir"
}

sha512sums="8d1ff775dd5c1abb8bb4c80aecacb544382e38d101f105fd0fd7eb6bf98ecb663dd533d16e7b1741d6e5ef0eb12a707ab90e0a7751356e625725f076b4dc1a30  gunicorn-19.9.0.tar.gz"
