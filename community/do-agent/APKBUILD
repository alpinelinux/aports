# Contributor: Dhruvin Gandhi <contact@dhru.vin>
# Maintainer: Will Sinatra <wpsinatra@gmail.com>
pkgname=do-agent
pkgver=3.18.8
_git_rev=adb71f08dee5f03de83beca48a3d052e84f0f3f8
pkgrel=0
pkgdesc="Collects system metrics from DigitalOcean Droplets"
url="https://github.com/digitalocean/do-agent"
arch="x86_64"  # limited by digital ocean
license="Apache-2.0"
makedepends="go"
install="$pkgname.pre-install"
subpackages="$pkgname-doc $pkgname-openrc"
source="https://github.com/digitalocean/do-agent/archive/refs/tags/$pkgver/do-agent-$pkgver.tar.gz
	do-agent.initd
	do-agent.confd"

build() {
	go build -ldflags "-X 'main.version=$pkgver' -X 'main.revision=$_git_rev' -X 'main.buildDate=$(date)'" \
		-o bin/do-agent ./cmd/do-agent
}

check() {
	go test -v ./...
}

package() {
	install -Dm755 bin/do-agent "$pkgdir/usr/sbin/do-agent"
	install -Dm644 LICENSE "$pkgdir/usr/share/doc/do-agent/LICENSE"
	install -Dm755 "$srcdir/do-agent.initd" "$pkgdir/etc/init.d/do-agent"
	install -Dm644 "$srcdir/do-agent.confd" "$pkgdir/etc/conf.d/do-agent"
}

sha512sums="
f32ed364df665515aab5d3af5165da428a2536d03d250fad2a73f290df2d90131f8448517c9b4db70bee148a7d38ce8970414eed4cc79fe43bece71363cc7b3d  do-agent-3.18.8.tar.gz
cb89ceeb17767f1cf30055649d7f1fa1496a6a763b23c501a440bea871d6f43abec8901d5ab18d96b170bb95c374c2f5e468fd12496178d218cd13ab299527c4  do-agent.initd
3ad8d9223de1c99c1f231ca9b6500f932c14fbea38093d28231c6c805632ae0594dfd20dba678061e07ae13e8b0d20cc79185fd05b17522f69b768178866347c  do-agent.confd
"
