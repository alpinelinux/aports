# Contributor: Guy Godfroy <guy.godfroy@gugod.fr>
# Maintainer: Guy Godfroy <guy.godfroy@gugod.fr>
pkgname=py3status
pkgver=3.47
pkgrel=0
pkgdesc="Extensible i3status wrapper written in python"
url="https://py3status.readthedocs.io"
arch="noarch"
license="BSD-3-Clause"
depends="python3 py3-setuptools"
checkdepends="py3-pytest"
subpackages="$pkgname-doc"
source="$pkgname-$pkgver.tar.gz::https://github.com/ultrabug/py3status/archive/refs/tags/$pkgver.tar.gz"

build() {
	python3 setup.py build
}

check() {
	pytest
}

package() {
	python3 setup.py install --prefix=/usr --root="$pkgdir"
	install -d "$pkgdir/usr/share/doc/$pkgname"
	install -d "$pkgdir/usr/share/doc/$pkgname/dev-guide"
	install -d "$pkgdir/usr/share/doc/$pkgname/user-guide"
	install -m644 docs/*.md README.md CHANGELOG "$pkgdir/usr/share/doc/$pkgname"
	install -m644 docs/dev-guide/* "$pkgdir/usr/share/doc/$pkgname/dev-guide"
	install -m644 docs/user-guide/* "$pkgdir/usr/share/doc/$pkgname/user-guide"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha512sums="
4c583685b37ec251bad824930c6935ccc5cf527d784f89f4ba222f53d987409337b4ca1aa3b7f0adc995e2494aee3ad9efb315fcc2578cab4996fa719f3310eb  py3status-3.47.tar.gz
"
